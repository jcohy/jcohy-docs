import org.asciidoctor.gradle.jvm.AsciidoctorJPlugin

/**
 * Produce Javadoc for all Spring Framework modules in "build/docs/javadoc"
 */
apply plugin: "com.jcohy.docs"
apply plugin: "com.jcohy.oss-upload"

ext{
	language = "zh-cn"
	component = project.name
	version = project.version
}

task aggregatedAsciidoctor(type: Copy) {
	project.rootProject.gradle.projectsEvaluated {

//		Set<Project> excludedPdfProjects = ['spring-security','reactor','spring-data-redis']

		Set<Project> excludedPdfProjects = ['spring-security','reactor','spring-boot']

		Set<Project> excludedDocsProjects = ['spring-data-mongodb','hibernate-orm','gradle-docs','checkstyle']

		Set<Project> docsProjects = rootProject.subprojects.findAll {it != project }
				.findAll { it.plugins.hasPlugin(AsciidoctorJPlugin) }
				.findAll{!excludedDocsProjects.contains(it.name)}

		Set<Project> docsPdfProject = docsProjects.findAll{!excludedPdfProjects.contains(it.name)}
		dependsOn docsProjects.asciidoctor
		dependsOn docsPdfProject.asciidoctorPdf
		destinationDir = project.file "${buildDir}"
		docsProjects.each {project ->
			if(project.tasks.names.contains("asciidoctorMultiPage")){
				dependsOn project.asciidoctorMultiPage
				from(project.asciidoctor.outputDir) {
					into project.name+"/"+project.version+"/htmlsingle"
				}
				from(project.asciidoctorMultiPage.outputDir) {
					into project.name+"/"+project.version+"/html5"
				}
			} else {
				from(project.asciidoctor.outputDir) {
					into project.name+"/"+project.version+"/html5"
				}
			}
			from(project.asciidoctorPdf.outputDir) {
				into project.name+"/"+project.version+"/pdf"
			}
		}
	}
}

alioss {
	accessKey = System.getenv("ACCESS_KEY")
	secretKey = System.getenv("SECRET_KEY")
}

task docsZip(type: Zip) {
	dependsOn asciidoctor,
			asciidoctorPdf

	duplicatesStrategy "fail"
	from(asciidoctor.outputDir) {
		into "reference/htmlsingle"
	}
	from(asciidoctorPdf.outputDir) {
		into "reference/pdf"
		include "index.pdf"
		rename { "jcohy-docs-reference.pdf" }
	}
}

artifacts {
	archives docsZip
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact docsZip
		}
	}
}
